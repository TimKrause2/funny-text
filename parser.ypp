%require "3.6"
%{
#include "parser.h"
%}

%define api.value.type {struct YYSTYPE}
%token <INT> INT
%token <STRING> STRING
%define parse.error verbose
%define parse.lac full
%define api.pure full
%locations
%param { yyscan_t scanner }

%code requires {
    typedef void* yyscan_t;
}

%code top {
    #include "qstring.h"
    #include <stdio.h>
}

%code {
    int yylex ( YYSTYPE * lvalp, YYLTYPE * llocp, yyscan_t scanner);
    void yyerror(YYLTYPE* yyllocp, yyscan_t unused, const char* msg);
    int parse_file(const char *filename, yyscan_t scanner);
}

%%

input:
    %empty
    | input statement
    ;
    
statement:
    q_string
    {
        /* move the global temp string to the root string list */
        AppendTempStringToGlobalString();
    }
    | STRING ':'
    {
        /* allocate a new list and add it to the global
        q_string_list list */
        AllocateGlobalStringList( $1 );
    }
    q_string_list ';'
    | '|' STRING '|'
    {
        if(parse_file( $2.c_str(), scanner )==0){
            YYABORT;
        }
    }
    ;

q_string:
    '<' q_string_content '>';

q_string_content:
    q_string_element
    | q_string_content q_string_element
    ;
    
q_string_element:
    STRING
    {
        /* add a string copy to the global temp string*/
        AppendCopyStringToGlobalTempString( $1 );
    }
    | '|' STRING '|'
    {
        /* add a substitution reference to the global temp string*/
        AppendRefStringToGlobalTempString( $2 );
    }
    ;

q_string_list:
    q_string_list_element
    | q_string_list q_string_list_element
    ;
    
q_string_list_element:
    INT
    {
        /* allocate a q_string_list_element */
        AllocateGlobalStringListElement( $1 );
    }
    q_string 
    { 
        /* move the global temp string to the end of the 
        global q_string_list list. */ 
        AppendTempStringToGlobalStringList();
    } ;
    
%%

